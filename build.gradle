plugins {
    id 'java'
}

def miraiDir = new File(buildDir, 'mirai')
def lapisDir = new File(buildDir, 'lapis')

group 'top.mrxiaom'
version System.getProperty("lapis.version", "2.15.0-RC")

repositories {
    maven {
        name = 'Huawei Cloud Mirror'
        url = 'https://repo.huaweicloud.com/repository/maven'
    }
    mavenCentral()
    maven {
        name = 'Mirai Snapshots'
        url = 'https://repo.mirai.mamoe.net/snapshots'
    }
}
dependencies {
    implementation(platform('net.mamoe:mirai-bom:' + version))
    implementation('net.mamoe:mirai-core')
    implementation('net.mamoe:mirai-console')
    implementation('net.mamoe:mirai-console-terminal')
}
tasks.register('clearLibs', Delete.class) {
    delete new File(buildDir, 'mirai')
}
tasks.register('copyResources', Copy.class) {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from new File(projectDir, 'src/main/resources')
    destinationDir miraiDir
}
tasks.register('generate', Copy.class) {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    configurations.named("runtimeClasspath").configure{
        it.files.forEach { from it }
    }
    destinationDir new File(miraiDir, 'libraries')
}
tasks.register("pack", Zip.class) {
    dependsOn('copyResources')
    dependsOn('generate')
    from miraiDir
    destinationDir lapisDir
    archiveFileName.set('Mirai一键包 ' + rootProject.version + '.zip')
}
